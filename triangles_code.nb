Manipulate[
 Module[{m3, triangles, colors}, 
 m3 = 1 - m1 - m2; (*make homogeneous*)
  colors = ColorData[scheme] /@ Range[0, 1, .2];
  triangles = 
   Nest[
     Flatten[
      make6Triangles[#, {m1, m2, m3}, {d1, d2, d3}] & /@ #, 1
    ] 
    &,
    make6Triangles[{pt1, pt2, pt3},
     If[ex1, {.33, .33, .33}, {m1, m2, m3}], 
     If[ex1, {.5, .5, .5}, {d1, d2, d3}]
    ]
    , n - 1
  ];
  Graphics[
   MapIndexed[{
    FaceForm[
      colors[[Mod[#2[[1]] - 1, 6] + 1]]], 
      If[edges, EdgeForm[{Thickness[.0025]}]], 
      Polygon[#1]} &, 
      triangles
      ], 
    Background -> White, 
   PlotRange -> {{-.02, 1.02}, {-.02, .9}}, ImageSize -> 465]],
 {{pt1, {0, 0}}, {0, 0}, {1, .89}, Locator},
 {{pt2, {1, 0}}, {0, 0}, {1, .89}, Locator},
 {{pt3, {.5, Sin[60 \[Degree]]}}, {0, 0}, {1, .89}, Locator},
 Grid[{{"iterations", Control[{{n, 3, ""}, Range[4], SetterBar}], 
    Control[{{ex1, True, "symmetric initial triangle"}, {False, 
       True}}],
    Control[{{scheme, "TemperatureMap", "colors"}, 
      Dynamic[# -> Show[ColorData[#, "Image"], ImageSize -> 150] & /@ 
        ColorData["Gradients"]], ControlType -> PopupMenu, 
      ImageSize -> {95, 20}}], SpanFromLeft},
   {"dividers", 
    Control[{{d1, .66, Subscript[Style["d", Italic], "1"]}, 0, 
      1, .001, ImageSize -> Tiny, Appearance -> "Labeled"}], 
    Control[{{d2, .21, Subscript[Style["d", Italic], "2"]}, 0, 
      1, .001, ImageSize -> Tiny, Appearance -> "Labeled"}], 
    Control[{{d3, .65, Subscript[Style["d", Italic], "3"]}, 0, 
      1, .001, ImageSize -> Tiny, Appearance -> "Labeled"}]},
   {"masses", 
    Control[{{m1, .45, Subscript[Style["m", Italic], "1"]}, 
      0, .667, .001, ImageSize -> Tiny, Appearance -> "Labeled"}], 
    Control[{{m2, .19, Subscript[Style["m", Italic], "2"]}, 
      0, .667, .001, ImageSize -> Tiny, Appearance -> "Labeled"}], 
    Row[{Dynamic[
       Row[{Subscript[Style["m", Italic], "3" ], "= ", 
         ToString[1 - m1 - m2]}]], Spacer[20], 
      Control[{edges, {True, False}}]}]
    }}, Alignment -> Left],
 TrackedSymbols :> True, SynchronousUpdating -> True, 
 AutorunSequencing -> {5, 7, 8, 9, 10, 11},
 Initialization :> (
   barycenter[{p1_, p2_, p3_}, {m1_, m2_, 
      m3_}] := {m1, m2, m3}.{p1, p2, p3};
   make6Triangles[{p1_, p2_, p3_}, {m1_, m2_, m3_}, {d1_, d2_, d3_}] :=
    Module[
      {p, q1, q2, q3}, 
      p = barycenter[{p1, p2, p3}, {m1, m2, m3}];
      {q1, q2, q3} = {d1 p1 + (1 - d1) p2, d2 p2 + (1 - d2) p3, (1 - d3) p1 + d3 p3}; 
      {{p, p1, q1},
      {p, p2, q1},
      {p, p2, q2},
      {p, p3, q2},
      {p, p3, q3},
      {p, p1, q3}
      }])]
